from pathlib import Path
from snakemake.utils import min_version


###


min_version("8.4.8")


configfile: "config/params.yaml"


onsuccess:
    print("Workflow finished succesfuly!")
    if config["send_email"]:
        shell(
            'mail -s "Workflow finished successfuly!" ' + config["email"] + " < {log}"
        )


onerror:
    print("An error occurred!")
    if config["send_email"]:
        shell('mail -s "An error occurred!" ' + config["email"] + " < {log}")


###


def aggregate_input(wildcards):
    """
    Aggregate input files for the downstream analysis rule
    after the checkpoint has been executed.
    """
    checkpoint_output = checkpoints.find_structural_homologs_SCOPe.get(
        **wildcards
    ).output[0]
    with open(checkpoint_output) as f:
        homologs = f.read().splitlines()
    aggregated_input = []
    aggregated_input += expand(
        "results/{pdb_id}/separate_RNA_structures/{homolog}/MFEs.txt",
        pdb_id=wildcards.pdb_id,
        homolog=homologs,
    )
    aggregated_input += expand(
        "results/{pdb_id}/consensus_RNA_structures/{homolog}/MFEs.txt",
        pdb_id=wildcards.pdb_id,
        homolog=homologs,
    )
    aggregated_input += expand(
        "results/{pdb_id}/structural_homologs.txt",
        pdb_id=wildcards.pdb_id,
    )
    aggregated_input += expand(
        "results/{pdb_id}/protein_sequences/combined.fasta",
        pdb_id=wildcards.pdb_id,
    )
    aggregated_input += expand(
        "results/{pdb_id}/structural_alignment/alignment.txt",
        pdb_id=wildcards.pdb_id,
    )
    return aggregated_input


# Determine the PDB IDs to process
with open("resources/list.txt") as f:
    PDB_IDs = f.read().lower().splitlines()


rule all:
    input:
        expand("results/{pdb_id}/protein_sequences/.done", pdb_id=PDB_IDs),
        expand("results/{pdb_id}/protein_structures/.done", pdb_id=PDB_IDs),
        expand("results/{pdb_id}/downstream_analysis/.done", pdb_id=PDB_IDs),


checkpoint find_structural_homologs_SCOPe:
    input:
        "resources/dir.cla.scope.2.08-stable.tsv",
    output:
        "results/{pdb_id}/structural_homologs.txt",
    threads: workflow.cores
    shell:
        """
        category=$(cat {input} | grep {wildcards.pdb_id} \
            | awk '{{print $4}}' | awk '!seen[$0]++')
        rm -f {output}.temp
        for c in $category; do
            homologs=$(cat {input} | grep $c \
                | awk '{{print $2}}' | awk '!seen[$0]++')
            echo $homologs | awk '{{gsub(" ", "\\n")}} 1' >> {output}.temp
        done
        awk '!seen[$0]++' {output}.temp > {output}
        rm {output}.temp
        """


rule download_protein_sequences_and_structures:
    input:
        rules.find_structural_homologs_SCOPe.output,
    output:
        "results/{pdb_id}/protein_sequences/.done",
        "results/{pdb_id}/protein_structures/.done",
        sequences=directory("results/{pdb_id}/protein_sequences"),
        structures=directory("results/{pdb_id}/protein_structures"),
        combined_sequences="results/{pdb_id}/protein_sequences/combined.fasta",
    threads: workflow.cores
    shell:
        """
        for e in $(cat {input}); do
            wget https://www.rcsb.org/fasta/entry/$e -q \
            -O {output.sequences}/$e.fasta
            wget https://files.rcsb.org/download/$e.pdb -q \
            -O {output.structures}/$e.pdb
        done
        cat {output.sequences}/*.fasta > {output.combined_sequences}
        touch {output.sequences}/.done
        touch {output.structures}/.done
        """


rule filter_protein_sequences:
    """
    Remove non-protein sequences from the combined file.
    """
    input:
        rules.download_protein_sequences_and_structures.output.combined_sequences,
    output:
        filtered="results/{pdb_id}/protein_sequences/filtered_combined.fasta",
    conda:
        "envs/biopython.yaml"
    threads: workflow.cores
    script:
        "scripts/filter_protein_sequences.py"


rule align_protein_structures:
    input:
        homologs=rules.find_structural_homologs_SCOPe.output,
        dir=rules.download_protein_sequences_and_structures.output.structures,
    output:
        "results/{pdb_id}/structural_alignment/alignment.txt",
        "results/{pdb_id}/structural_alignment/.done",
        dir=directory("results/{pdb_id}/structural_alignment/"),
    threads: workflow.cores
    singularity:
        "docker://f1lem0n/usalign:latest"
    params:
        split=config["USalign"]["split"],
        full=config["USalign"]["full"],
    shell:
        """
        # Run USalign
        USalign -dir {input.dir} {input.homologs} \
            -suffix .pdb -mm 4 -full {params.full} -o {output.dir}/sup \
            -split {params.split} > {output.dir}/alignment.txt

        # Remove non-ATOM lines from superposed PDB files
        for s in $(find {output.dir} -name "sup*.pdb"); do
            awk '{{if($1=="ATOM") print $0}}' $s > $s.atoms
            rm $s
            mv $s.atoms $s
        done

        # Clean up and create .done file
        rm -f *.pml
        rm -f .pdb
        touch {output.dir}/.done
        """


rule tblastn_protein_sequences:
    input:
        rules.filter_protein_sequences.output,
    output:
        "results/{pdb_id}/tblastn_results.txt",
    singularity:
        "docker://ncbi/blast"
    params:
        evalue=config["blast"]["e_value"],
    threads: workflow.cores
    log:
        "logs/{pdb_id}/tblastn.log",
    shell:
        """
        BLASTDB=$(pwd)/database/refseq_rna:$BLASTDB

        tblastn -query {input} -out {output} \
            -outfmt 6 -max_target_seqs 100 -evalue {params.evalue} \
            -num_threads {threads} -db refseq_rna &> {log}
        """


rule download_nucleotide_sequences:
    input:
        rules.tblastn_protein_sequences.output,
    output:
        dir=directory("results/{pdb_id}/nucleotide_sequences/{homolog}"),
        combined_sequences="results/{pdb_id}/nucleotide_sequences/{homolog}/combined.fasta",
    conda:
        "envs/biopython.yaml"
    threads: workflow.cores
    script:
        "scripts/download_nucleotide_sequences.py"


rule align_nucleotide_sequences:
    input:
        rules.download_nucleotide_sequences.output.combined_sequences,
    output:
        alignment="results/{pdb_id}/nucleotide_alignments/{homolog}/alignment.txt",
        dir=directory("results/{pdb_id}/nucleotide_alignments/{homolog}"),
    singularity:
        "docker://biocontainers/clustalw:v2.1lgpl-6-deb_cv1"
    threads: workflow.cores
    log:
        "logs/{pdb_id}/align_nucleotide_sequences_{homolog}.log",
    shell:
        """
        clustalw -INFILE={input} -OUTFILE={output.alignment} &> {log} \
        || echo "ERROR: clustalw failed" > {output.alignment}
        """


rule predict_separate_RNA_structures:
    input:
        rules.download_nucleotide_sequences.output.combined_sequences,
    output:
        "results/{pdb_id}/separate_RNA_structures/{homolog}/.done",
        mfe="results/{pdb_id}/separate_RNA_structures/{homolog}/MFEs.txt",
        dir=directory("results/{pdb_id}/separate_RNA_structures/{homolog}"),
    conda:
        "envs/viennaRNA.yaml"
    params:
        args_string=config["RNAfold"],
        dpi=config["plots"]["DPI"],
        mountain_script=Path("workflow/scripts/mountain.pl").absolute(),
        relplot_script=Path("workflow/scripts/relplot.pl").absolute(),
    threads: workflow.cores
    log:
        "logs/{pdb_id}/predict_separate_RNA_structures_{homolog}.log",
    shell:
        """
        input=$(pwd)/{input}
        cd {output.dir}
        RNAfold {params.args_string} -j {threads} -i $input > MFEs.txt 2> {log} && \
        mkdir -p structures && \
        mkdir -p dotplots && \
        for f in $(ls *_dp.ps); do
            sample=$(basename --suffix="_dp.ps" $f)
            perl {params.relplot_script} $sample"_ss.ps" $sample"_dp.ps" \
                > $sample"_css".ps
            perl {params.mountain_script} $sample"_dp.ps" > mountain.tsv
            gs -dNOPAUSE -dBATCH -sDEVICE=png16m -r{params.dpi} \
                -sOutputFile=dotplots/$sample"_dp".png $sample"_dp.ps" &>> {log}
            gs -dNOPAUSE -dBATCH -sDEVICE=png16m -r{params.dpi} \
                -sOutputFile=structures/$sample"_css".png $sample"_css.ps" &>> {log}
        done || touch MFEs.txt \
        touch .done
        cd -
        """


rule predict_consensus_RNA_structure:
    input:
        rules.align_nucleotide_sequences.output.alignment,
    output:
        "results/{pdb_id}/consensus_RNA_structures/{homolog}/MFEs.txt",
        dir=directory("results/{pdb_id}/consensus_RNA_structures/{homolog}"),
    conda:
        "envs/viennaRNA.yaml"
    params:
        args_string=config["RNAalifold"],
        dpi=config["plots"]["DPI"],
        mountain_script=Path("workflow/scripts/cmount.pl").absolute(),
    threads: workflow.cores
    log:
        "logs/{pdb_id}/predict_consensus_RNA_structure_{homolog}.log",
    shell:
        """
        cd {output.dir}
        RNAalifold {params.args_string} < {input} > MFEs.txt 2> {log} \
        && perl {params.mountain_script} alidot.ps > mountain.ps \
        && gs -dNOPAUSE -dBATCH -sDEVICE=png16m -r{params.dpi} \
            -sOutputFile=alidot.png alidot.ps &>> {log} \
        && gs -dNOPAUSE -dBATCH -sDEVICE=png16m -r{params.dpi} \
            -sOutputFile=alirna.png alirna.ps &>> {log} \
        && gs -dNOPAUSE -dBATCH -sDEVICE=png16m -r{params.dpi} \
            -sOutputFile=mountain.png mountain.ps &>> {log} \
        || touch MFEs.txt
        cd -
        """


rule downstream_analysis:
    input:
        aggregate_input,
    output:
        "results/{pdb_id}/downstream_analysis/.done",
    conda:
        "envs/dplyr.yaml"
    threads: workflow.cores
    script:
        "scripts/downstream_analysis.R"
